plugins {
    id 'org.springframework.boot' version "${springBootVersion}"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'
    group = 'com.ab.tasktracker'
    version = '1.0'

    sourceCompatibility = 21
    targetCompatibility = 21

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs << "-parameters"  // To preserve args name during compilation which helps in SPEL
    }


    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
        }
    }


    dependencies {
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation group: 'junit', name: 'junit', version: "$junitVersion"
        testImplementation "org.mockito:mockito-core:$mockitoVersion"
        implementation "com.googlecode.json-simple:json-simple:$jsonSimpleVersion"
    }
}
